plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'idea'
    id 'com.github.node-gradle.node' version '3.0.1'
}

group 'com.github.srwaggon'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

node {
    download = true
    version = '14.15.5'
    nodeProjectDir = file("${project.projectDir}/src/main/javascript/adventure")
    yarnVersion = "1.22.10"
    yarnWorkDir = file("${project.projectDir}/src/main/javascript/adventure")
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task buildReactApp(type: YarnTask, dependsOn: 'npmInstall') {
    args = ['build']
}

clean.delete << file('src/main/javascript/adventure/build')
clean.delete << file('src/main/javascript/adventure/node_modules')
clean.delete << file('src/main/resources/static')

task copyReactApp(type: Copy) {
    from file("src/main/javascript/adventure/build")
    into file("src/main/resources/static")
}
copyReactApp.mustRunAfter 'buildReactApp'

processResources.dependsOn 'buildReactApp'
processResources.dependsOn 'copyReactApp'
